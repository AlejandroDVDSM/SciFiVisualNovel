title: Legion_Corpse
tags:
position: -6,-385
---
<<setsprite Scene Corpse>>

<<if !$player_knows_cat>>
  <<destroy SpawnManager Captain>>
  <<destroy SpawnManager Mordin>>
  <<place SpawnManager Mordin left>>
  <<place SpawnManager Kaidan right>>
  <<setsprite Kaidan Unknown>>
  ???: ...
  <<fadein Panel>>
  Mordin: Kaidan, cuéntale lo que me has contado al capitán.
  <<setsprite Kaidan Sad>>
  Kaidan: ...
  <<destroy SpawnManager Mordin>>
  <<place SpawnManager Captain left>>
  -> *Insistir a Kaidan*
    <<play AudioManager Click>>
    <<jump Kaidan_Tells_Story>>
  -> *Escuchar lo sucedido por parte de Mordin*
    <<play AudioManager Click>>
    <<jump Mordin_Tells_Story>>
<<else>>
  <<place SpawnManager Captain left>>
  <<setsprite Captain Surprised>>
  Tú: ¿Y el resto de la tripulación?
  <<setsprite Captain Neutral>>

  <<if visited("Kaidan_Tells_Story")>>
    <<destroy SpawnManager Kaidan>>
    <<place SpawnManager Mordin right>>
  <<endif>>

  Mordin: Seguirán durmiendo. Creo que debemos convocar una reunión y contarles.
  -> *Mantener oculto la muerte de Legión*
    <<play AudioManager Click>>
    <<jump Hide_Death>>
  -> *Contar todo al resto de la tripulación*
    <<play AudioManager Click>>
    <<jump Dont_Hide_Death>>
<<endif>>


===

title: Kaidan_Tells_Story
tags: 
position: -296,-242
---
<<if !$player_knows_who_died>>
  <<jump Player_Recognizes_Corpse>>
<<endif>>

<<setsprite Captain Angry>>
Tú: ¡Kaidan, pies en tierra! ¿¡Qué es lo que ha pasado!?
<<setsprite Kaidan Scared>>
Kaidan: Señor...
<<setsprite Captain Neutral>>
Kaidan: He ido a por un café porque no soy persona hasta que me tomo uno y de camino a la cocina me he encontrado a Legión ahí...
Kaidan: Muerto...
<<setsprite Kaidan Sad>>
Kaidan: Me he quedado paralizado hasta que Mordin apareció.
<<setsprite Kaidan Surprised>>
Kaidan: Lo más extraño de todo es que me pareció ver una figura alejándose del cadáver, pero no pude apreciarlo bien.
<<jump Cat_Appearance>>
===

title: Mordin_Tells_Story
tags: 
position: 282,-244
---
<<if !$player_knows_who_died>>
  <<setsprite Captain Sad>>
  Tú: Parece que está en shock... 
  <<jump Player_Recognizes_Corpse>>
<<endif>>

Tú: Será mejor que me lo cuentes tú, Mordin.
<<destroy SpawnManager Kaidan>>
<<place SpawnManager Mordin right>>
Mordin: Kaidan se dirigía a la cocina cuando de camino se encontró el cadáver de Legión.
Mordin: Poco tiempo después, llegué y me contó que vio una figura extraña alejarse del cadáver.
<<jump Cat_Appearance>>
===

title: Player_Recognizes_Corpse
tags:
position: -3,-41
---
  <<setsprite Captain Sad>>
  Tú: Joder... el cadáver es Legión.
  <<set $player_knows_who_died to true>>
  Tú: Justo hace dos semanas celebrábamos que hacía su primer mes con nosotros en la tripulación.
  <<setsprite Captain Angry>>
  Tú: Era su primera misión de exploración fuera de la colonia de "Palaven".
  <<setsprite Captain Neutral>>
  Tú: Bueno... mejor un androide de última generación que uno de nosotros, ¿no?

  <<if visited("Kaidan_Tells_Story")>>
    <<jump Kaidan_Tells_Story>>
  <<elseif visited("Mordin_Tells_Story")>>
    <<jump Mordin_Tells_Story>>
  <<endif>>
===

title: Cat_Appearance
tags:
position: -5,-242
---
<<play AudioManager Noises>>
???: *Suena algo de fondo*
<<setsprite Captain Surprised>>

<<if visited("Kaidan_Tells_Story")>>
  <<setsprite Kaidan Scared>>
<<elseif visited("Mordin_Tells_Story")>>
  <<setsprite Mordin Scared>>
<<endif>>

Tú: ¿Qué ha sido eso?
<<destroy SpawnManager Captain>>
<<place SpawnManager McFlurry left>>
<<setsprite McFlurry Unknown>>
<<set $player_knows_cat to true>>
<<play AudioManager Cat>>
???: Miau

<<if visited("Kaidan_Tells_Story")>> 
  <<setsprite Kaidan Happy>>
  Kaidan: Con todo lo que está pasando me he olvidado de McFlurry.
  <<setsprite McFlurry Neutral>>
  Kaidan: ¿Sabes? Seguramente la figura que no llegué a apreciar bien fuese él.
  <<setsprite Kaidan Neutral>>
<<elseif visited("Mordin_Tells_Story")>>
  <<setsprite Mordin Surprised>>
  <<setsprite McFlurry Neutral>>
  Mordin: ¿McFlurry? Pensé que estaría en el dormitorio de Kaidan.
  Mordin: ¿Sabes? Seguramente la figura que Kaidan no apreció bien fuese la del gato.
  <<setsprite Mordin Neutral>>
<<endif>>

<<setsprite McFlurry Happy>>
<<play AudioManager Cat>>
McFlurry: Miau
<<destroy SpawnManager McFlurry>>

<<jump Legion_Corpse>>
===

title: Hide_Death
tags:
position: 211,-541
---
Tú: Por ahora, no creo que debamos contarle a nadie lo sucedido.
<<set $crew_knows_legion_status to false>>
<<setsprite Mordin Angry>>
Mordin: ¿¡Qué!?
<<destroy SpawnManager Captain>>
<<place SpawnManager Kaidan left>>
Kaidan: En realidad...
Kaidan: Creo que sería lo más sensato.
Mordin: No me creo que se os esté pasando si quiera por la cabeza todo esto...
Mordin: ¿Del capitán? Bueno, podría creérmelo...
<<setsprite Mordin Sad>>
Mordin: ¿Pero de ti, Kaidan? Ni siquiera pareces el mismo de siempre.
<<setsprite Kaidan Sad>>
<<setsprite Mordin Neutral>>
Modin: En fin, la cadena de mando es la cadena de mando así que no me queda de otra que seguir las órdenes.
<<destroy SpawnManager Kaidan>>
<<place SpawnManager Captain left>>
Tú: Mordin, si las palabras son de plata, el silencio es de oro.
<<jump Choose_Destination>>
===

title: Dont_Hide_Death
tags:
position: -238,-543
---
Tú: Creo que lo más sensato sería que todos en la nave lo supiesen.
<<set $crew_knows_legion_status to true>>
<<setsprite Mordin Happy>>
Mordin: Concuerdo.
<<destroy SpawnManager Mordin>>
<<place SpawnManager Kaidan right>>
Kaidan: ¿Estás seguro? Lo único que conseguiremos con esto es que cunda el pánico.
Kaidan: Quiero decir...
Kaidan: Legión ya está muerto, ¿de qué va a servir contarles?
<<destroy SpawnManager Captain>>
<<place SpawnManager Mordin left>>
<<setsprite Mordin Angry>>
Mordin: ¿Se puede saber qué cojones estás diciendo? No pareces tú mismo. Estás fuera de tus cabales.
<<setsprite Kaidan Sad>>
Kaidan: ...
<<jump Choose_Destination>>
===

title: Choose_Destination
tags:
position: -18,-673
---
<<if visited("Dont_Hide_Death")>>
  <<destroy SpawnManager Mordin>>
  <<destroy SpawnManager Kaidan>>
  <<place SpawnManager Captain left>>
  <<place SpawnManager Mordin right>>
<<endif>>

Mordin: Señor, aún queda el asunto de la misión.
Tú: ¿Qué pasa? La Marina nos envió a Tuchanka para seguir explorando lo que la tripulación de la Normandía fue incapaz.
Mordin: Creo que deberíamos suspenderlo temporalmente para volver a Palaven y analizar la caja negra de Legión.
-> *Suspender la misión para volver a Palaven*
  <<play AudioManager Click>>
  <<set $destination to "Palaven">>
  <<if $crew_knows_legion_status == true>>
    <<fadeout Panel>>
    Tú: Convoquemos ya la reunión y contémosle a todos lo de Legión y el cambio de rumbo.
    <<jump Reunion>>
  <<else>>
    Tú: Hablaré con la teniente de vuelo para que fije rumbo a Palaven.
    <<jump Mordin_Insist>>
  <<endif>>
-> *Continuar hacia Tuchanka*
  <<play AudioManager Click>>
  <<set $destination to "Tuchanka">>
  <<if $crew_knows_legion_status == true>>
    <<fadeout Panel>>
    Tú: Convoquemos ya la reunión y contémosle a todos lo de Legión.
    <<jump Reunion>>
  <<else>>
    Tú: Deberíamos acabar la misión.
    <<jump Mordin_Insist>>
  <<endif>>
===

title: Mordin_Insist
tags:
---
  Mordin: Señor, se que accedió a ocultar la muerte de Legión por ahora pero...
  <<setsprite Mordin Surprised>>
  Mordin: ¿Podría al menos contárselo a Miranda? La jefa de seguridad debería saber lo sucedido.
  -> *Contárselo a Miranda*
    <<play AudioManager Click>>
    <<set $miranda_knows_legion_status to true>>
    <<setsprite Mordin Happy>>
    <<fadeout Panel>>
    Tú: Es lo que había pensado.
    <<jump Reunion>>
  -> *Ocultárselo a Miranda*
    <<play AudioManager Click>>
    <<set $miranda_knows_legion_status to false>>
    <<setsprite Mordin Angry>>
    Tú: Creo que no será necesario.
    <<jump Reunion>>

===