title: Reunion
tags:
position: 13,-274
---
<<destroy SpawnManager Captain>>
<<destroy SpawnManager Mordin>>

<<if $crew_knows_legion_status == true>>
  <<jump Reunion_Everyone>>
<<else>>
  <<if $miranda_knows_legion_status == true>>
    <<fadeout Panel>>
    ¿A quién deseas visitar?
    -> Miranda, la jefa de seguridad
      <<play AudioManager Click>>
      <<jump Reunion_Miranda>>
    -> Ashley, la teniente de vuelo <<if $destination == "Palaven">>
      <<play AudioManager Click>>
      <<jump Reunion_Ashley>>
  <<else>>
    <<if $destination == "Palaven">>
      <<fadeout Panel>>
      Tú: Iré a avisar a Ashley para que cambie de rumbo.
      <<jump Ashley_Lives>>
    <<else>>
      <<jump No_Reunion>>
    <<endif>>
  <<endif>>
<<endif>>
===

title: Reunion_Everyone
tags:
position: -266,-275
---
<<setsprite Scene WideShot>>
<<fadein Panel>>

<<place SpawnManager Captain Left>>
Tú: Os he reunido a todos para contaros sobre la muerte de Legión.
Tú: Kaidan lo ha encontrado hace menos de una hora.
Tú: Según él, vio una sombra alejarse del cadáver, pero seguramente haya sido McFlurry.
NOTA: REUNIÓN CON TODOS. SEGUIR DESARROLLANDO

===

title: No_Reunion
tags:
position: 352,-273
---
NOTA: NI MIRANDA NI LA TIPULACIÓN SABEN NADA SOBRE LA MUERTE DE LEGIÓN. SEGUIR DESARROLLANDO

===

title: Reunion_Miranda
tags:
position: -120,0
---
<<setsprite Scene FrontDoor>>
<<place SpawnManager Captain left>>
<<fadein Panel>>
<<play AudioManager Knock_Door>>
Tú: ¿Puedo pasar?
<<fadeout Panel>>
???: Claro, entra.
<<setsprite Scene FrontDoorInside>>
<<fadein Panel>>
<<place SpawnManager Miranda right>>
<<setsprite Miranda Unknown>>
???: ¿Necesitas algo?
Tú: No, Miranda. Vengo a ponerte al día.
<<setsprite Miranda Surprised>>
Miranda: ¿Todo bien?
<<setsprite Miranda Neutral>>
<<setsprite Captain Sad>>
Tú: No. Esto que te voy a contar solo lo saben Mordin y Kaidan así que te pido que lo mantengas oculto por el momento.
Tú: Kaidan ha encontrado el cadáver de Legión.
<<setsprite Miranda Surprised>>
Miranda: ¿Se sabe quién ha sido? ¿Porqué y cómo?
Tú: No sabemos nada.
<<setsprite Miranda Neutral>>
<<setsprite Captain Neutral>>

<<if $destination == "Palaven">>
  Tú: Una última cosa: he decidido que suspender la misión es lo más conveniente.
  Tú: Volveremos a Palaven.
  <<fadeout Panel>>
  <<destroy SpawnManager Miranda>>
  Tú: Ahora que estás al tanto de todo yo iré a contarle a Ashley el cambio de rumbo.
  <<jump Reunion_Ashley>>

<<elseif $destination == "Tuchanka">>
  Tú: La misión sigue en pie, así que tendremos que resolver esto de camino a Tuchanka.
  <<fadeout Panel>>
  <<destroy SpawnManager Miranda>>
  <<jump Reunion_Ashley>>
<<endif>>
===

title: Reunion_Ashley
tags:
position: 128,0
---
<<if visited("Reunion_Miranda")>>
  <<jump Ashley_Dies>>
<<else>>
  <<jump Ashley_Lives>>
<<endif>>
===

title: Ashley_Dies
tags:
position: 35,186
---
<<setsprite Scene FrontDoor>>
<<fadein Panel>>
<<play AudioManager Alarm>>
Tú: ¡Mierda, la alarma!
<<fadeout Panel>>
Altavoz: *NECESITO AYUDA URGENTEMENTE EN LA CABINA DE LA NAVE*
<<setsprite Scene Cabin>>
<<fadein Panel>>
<<destroy SpawnManager Captain>>
<<place SpawnManager Ashley left>>
<<setsprite Ashley Unknown>>
<<place SpawnManager BabySaren right>>
???: AYUDA
<<destroy SpawnManager Ashley>>
<<place SpawnManager Captain left>>
<<setsprite Captain Angry>>
Tú: ¡Ashley, apártate y podré dispararle!
<<play AudioManager Shotgun>>
<<setsprite BabySaren Sad>>
???: ¡URGH!
<<destroy SpawnManager BabySaren>>
<<setsprite Captain Happy>>
Tú: ¡Le he dado!
<<setsprite Captain Angry>>
Tú: ¡Pero se ha largado!
<<setsprite Captain Sad>>
Tú: ¿Ashley?
Tú: Mierda, he llegado tarde...
<<place SpawnManager Tali right>>
<<setsprite Tali Unknown>>
???: Joder, ¿qué era eso? ¿Estáis bien?
<<setsprite Tali Scared>>
Tú: Ashley ha muerto...
<<setsprite Captain Angry>>
Tú: ¡Tali! ¡Ve a por Miranda y persigue a esa cosa!
<<setsprite Tali Neutral>>
Tali: Estoy en ello.

-> *Dar caza al enemigo*
  <<play AudioManager Click>>
  Tú: Yo iré a por el enemigo.
  <<if $destination == "Palaven">>
    Tú: Mientras tanto, fija rumbo a Palaven.
  <<endif>>
  <<fadeout Panel>>
  Tú: También, incrementa la velocidad de la nave. Luego, ve a por Miranda y dile que se ponga su traje de combate.
  <<destroy SpawnManager Tali>> 
  <<jump Player_Hunts_BabySaren>>

-> *Convocar reunión*
  <<play AudioManager Click>>
  Tú: Voy a convocar una reunión para alertarlos a todos.
  <<if $destination == "Palaven">>
    Tú: Mientras tanto, fija rumbo a Palaven.
  <<endif>>
  <<fadeout Panel>>
  Tú: También, incrementa la velocidad de la nave. Luego, ve a por Miranda y dile que se ponga su traje de combate.
  <<destroy SpawnManager Tali>>
  <<jump Reunion_Everyone>>
===

title: Ashley_Lives
tags:
position: 214,186
---
<<setsprite Scene Cabin>>
<<fadein Panel>>
<<place SpawnManager Ashley right>>
<<setsprite Ashley Unknown>>
<<place SpawnManager Captain left>>
???: ¿Capitán? A esta hora imaginé que aún estaría durmiendo.
Tú: Buenos días, Ashley. Necesito que fijes rumbo a Palaven.
<<setsprite Ashley Surprised>>
Ashley: ¿A Palaven? ¿Y qué pasa con la misión? ¿No deberíamos ir a Tuchanka?
<<setsprite Ashley Neutral>>
Tú: Mmmm...
Tú: La misión ha sido suspendida hasta nueva orden por la marina.
<<play AudioManager Noises>>
???: *Suena algo de fondo*
Tú: ¿Otra vez McFlurry?
<<destroy SpawnManager Captain>>
<<destroy SpawnManager Ashley>>
<<place SpawnManager Ashley left>>
<<setsprite Ashley Scared>>
<<place SpawnManager BabySaren right>>
Ashley: ¡Esa cosa no es McFlurry!
Ashley: ¡Urgh!
Ashley: ¡Mi pierna!
<<setsprite Ashley Angry>>
<<play AudioManager Alarm>>
<<destroy SpawnManager Ashley>>
<<place SpawnManager Captain left>>
<<setsprite Captain Angry>>
Tú: *A través del altavoz* ¡NECESITAMOS AYUDA URGENTEMENTE EN LA CABINA!
Tú: He hecho sonar la alarma. 
Tú: Ashley, hazte a un lado y podré dispararle.
<<play AudioManager Shotgun>>
<<setsprite BabySaren Sad>>
???: ¡URGH!
<<destroy SpawnManager BabySaren>>
<<setsprite Captain Happy>>
Tú: ¡Le he dado!
<<setsprite Captain Angry>>
Tú: ¡Pero se ha largado!
<<place SpawnManager Tali right>>
<<setsprite Tali Unknown>>
???: Joder, ¿qué era eso? ¿Estáis bien?
<<setsprite Tali Scared>>
Tú: ¡Tali! ¡Ve a por Miranda y persigue a esa cosa!
<<setsprite Tali Neutral>>
Tali: Estoy en ello.
<<destroy SpawnManager Tali>>
<<place SpawnManager Ashley right>>
<<setsprite Ashley Scared>>
Tú: Ashley, ¿estás bien?
Ashley: ¡No, claro que no estoy bien!
Ashley: Una mierda andante me ha atacado. y me ha destrozado la pierna derecha.
Ashley: ¿Qué carajos era eso, Capitán?
<<setsprite Captain Sad>>
Tú: No lo sabemos, pero ha acabado con Legión.
Tú: Lo he mantenido oculto porque pensé que era buena idea.
<<setsprite Ashley Angry>>
Ashley: ¿¡Nos lo ocultaste!?
-> *Dar caza al enemigo*
  <<play AudioManager Click>>
  <<fadeout Panel>>
  Tú: Yo iré a por el enemigo mientras Tali va a por Miranda.
  <<destroy SpawnManager Ashley>> 
  <<jump Player_Hunts_BabySaren>>
-> *Convocar reunión*
  <<play AudioManager Click>>
  <<fadeout Panel>>
  Tú: Mientras Tali va a por Miranda, notificaré a todos de lo sucedido.
  <<jump Reunion_Everyone>>
===

title: Player_Hunts_BabySaren
tags:
---
<<setsprite Scene FrontDoor>>
<<fadein Panel>>
Tú: ¿Dónde se ha metido?
Tú: Juraría haberlo visto girar hacia aquí.
<<place SpawnManager Saren right>>
<<setsprite Saren Unknown>>
???: Aquí estoy...

-> *Disparar*
  <<play AudioManager Click>>
  <<play AudioManager Shotgun>>
  <<setsprite Captain Angry>>
  ???: Esta vez no has tenido tanta suerte de acertar.
  Tú: ¿Y tú quién eres ahora?
  ???: Saren. Ese es mi nombre.
  <<setsprite Saren Angry>>
  <<play AudioManager Shotgun>>
  Tú: Que te den, Saren.
  <<destroy SpawnManager Saren>>
  <<play AudioManager ShipLanding>>
  <<if $destination == "Palaven">>
    Tú: Parece que ya hemos llegado a Palaven.
  <<else>>
    Tú: Parece que ya hemos llegado a Tuchanka.
  <<endif>>
  Tú: Mierda, ha vuelto a escapar.
  Tú: Lo mejor será convocar ya la reunión.

-> *Dialogar*
  <<play AudioManager Click>>
  <<setsprite Captain Angry>>
  Tú: ¿Y tú quién eres ahora?
  ???: Saren. Ese es mi nombre.
  <<setsprite Saren Neutral>>
  <<if visited("Ashley_Dies")>>
    Tú: ¿Eres amigo del que asesinó a Legión y a Ashley?
  <<else>>
    Tú: ¿Eres amigo del que asesinó a Legión y atacó a Ashley?
  <<endif>>
  Saren: Yo soy quién los asesinó. 
  Saren: Mi ciclo vital por fin se ha completado y he evolucionado a lo que estás viendo.
  Saren: Beneficios de mi raza.
  <<destroy SpawnManager Saren>>
  <<play AudioManager ShipLanding>>
  <<if $destination == "Palaven">>
    Tú: Parece que ya hemos llegado a Palaven.
  <<else>>
    Tú: Parece que ya hemos llegado a Tuchanka.
  <<endif>>
  Tú: Mierda, ha vuelto a escapar.
  Tú: Lo mejor será convocar ya la reunión.

===